// Don't run against the included builds (if the main build has any).
def isTopLevelBuild = gradle.getParent() == null
if (isTopLevelBuild) {
    settingsEvaluated { settings ->
        settings.buildCache {
            local {
                enabled = false
            }
            remote(HttpBuildCache) {
                enabled = true
                push = false
                def remoteBuildCacheUrl = remoteBuildCacheUrl
                if (remoteBuildCacheUrl) {
                    url = withPathTrailingSlash(remoteBuildCacheUrl)
                }
            }
        }
    }
}

URI getRemoteBuildCacheUrl() {
    def projectProperties = gradle.startParameter.projectProperties
    projectProperties.containsKey("com.gradle.enterprise.build_validation.remoteBuildCacheUrl")
            ? new URI(projectProperties.get("com.gradle.enterprise.build_validation.remoteBuildCacheUrl"))
            : null
}

static URI withPathTrailingSlash(URI uri) {
    uri.path.endsWith("/") ? uri : new URI(uri.scheme, uri.userInfo, uri.host, uri.port, uri.path + "/", uri.query, uri.fragment)
}
