import org.gradle.util.GradleVersion
import java.nio.charset.StandardCharsets

import static java.lang.Boolean.parseBoolean

// note that there is no mechanism to share code between the initscript{} block and the main script, so some logic is duplicated

// conditionally apply the GE / Build Scan plugin to the classpath so it can be applied to the build further down in this script
initscript {
    def isTopLevelBuild = !gradle.parent
    if (!isTopLevelBuild) {
        return
    }

    // the way this closure reads system properties means they are not tracked as inputs to the configuration cache model,
    // the same is the case when reading environment variables with Gradle [6.5, 7.3]
    def getInputParam = { String name ->
        def envVarName = name.toUpperCase().replace('.', '_').replace('-', '_')
        return gradle.startParameter.systemPropertiesArgs[name] ?: System.getenv(envVarName)
    }

    // the following local variables do not change between the build invocations of the same experiment run
    // thus, reading their values in way that the configuration cache does not track them is acceptable
    def pluginRepositoryUrl = getInputParam('com.gradle.enterprise.build-validation.gradle.plugin-repository.url')
    def gePluginVersion = getInputParam('com.gradle.enterprise.build-validation.gradle-enterprise.plugin.version')
    def ccudPluginVersion = getInputParam('com.gradle.enterprise.build-validation.ccud.plugin.version')

    def atLeastGradle5 = GradleVersion.current() >= GradleVersion.version('5.0')
    def atLeastGradle4 = GradleVersion.current() >= GradleVersion.version('4.0')

    if (gePluginVersion || ccudPluginVersion && atLeastGradle4) {
        pluginRepositoryUrl = pluginRepositoryUrl ?: 'https://plugins.gradle.org/m2'
        logger.quiet("Gradle Enterprise plugins resolution: $pluginRepositoryUrl")

        repositories {
            maven { url pluginRepositoryUrl }
        }
    }

    dependencies {
        if (gePluginVersion) {
            classpath atLeastGradle5 ?
                    "com.gradle:gradle-enterprise-gradle-plugin:$gePluginVersion" :
                    "com.gradle:build-scan-plugin:1.16"
        }

        if (ccudPluginVersion && atLeastGradle4) {
            classpath "com.gradle:common-custom-user-data-gradle-plugin:$ccudPluginVersion"
        }
    }
}

def BUILD_SCAN_PLUGIN_ID = 'com.gradle.build-scan'
def BUILD_SCAN_PLUGIN_CLASS = 'com.gradle.scan.plugin.BuildScanPlugin'

def GRADLE_ENTERPRISE_PLUGIN_ID = 'com.gradle.enterprise'
def GRADLE_ENTERPRISE_PLUGIN_CLASS = 'com.gradle.enterprise.gradleplugin.GradleEnterprisePlugin'
def GRADLE_ENTERPRISE_EXTENSION_CLASS = 'com.gradle.enterprise.gradleplugin.GradleEnterpriseExtension'

def CCUD_PLUGIN_ID = 'com.gradle.common-custom-user-data-gradle-plugin'
def CCUD_PLUGIN_CLASS = 'com.gradle.CommonCustomUserDataGradlePlugin'

def isTopLevelBuild = !gradle.parent
if (!isTopLevelBuild) {
    return
}

// the way this closure reads system properties means they are not tracked as inputs to the configuration cache model,
// the same is the case when reading environment variables with Gradle [6.5, 7.3]
def getInputParam = { String name ->
    def envVarName = name.toUpperCase().replace('.', '_').replace('-', '_')
    return gradle.startParameter.systemPropertiesArgs[name] ?: System.getenv(envVarName)
}

// the following local variables do not change between the build invocations of the same experiment run
// thus, reading their values in way that the configuration cache does not track them is acceptable
def geUrl = getInputParam('com.gradle.enterprise.build-validation.gradle-enterprise.url')
def geAllowUntrustedServer = getInputParam('com.gradle.enterprise.build-validation.gradle-enterprise.allow-untrusted-server')
def gePluginVersion = getInputParam('com.gradle.enterprise.build-validation.gradle-enterprise.plugin.version')
def ccudPluginVersion = getInputParam('com.gradle.enterprise.build-validation.ccud.plugin.version')

def expDir = getInputParam('com.gradle.enterprise.build-validation.expDir')
def expId = getInputParam('com.gradle.enterprise.build-validation.expId')
def runId = getInputParam('com.gradle.enterprise.build-validation.runId')
def scriptsVersion = getInputParam('com.gradle.enterprise.build-validation.scriptsVersion')
def omitServerUrlValidation = parseBoolean(getInputParam('com.gradle.enterprise.build-validation.omitServerUrlValidation'))

def atLeastGradle4 = GradleVersion.current() >= GradleVersion.version('4.0')

// finish early if configuration parameters passed in via system properties are not valid/supported
if (ccudPluginVersion && isNotAtLeast(ccudPluginVersion, '1.7')) {
    logger.warn("Common Custom User Data Gradle plugin must be at least 1.7. Configured version is $ccudPluginVersion.")
    return
}

// register build scan listeners to capture build scan URL/id and to track publishing errors
def registerBuildScanActions = { def buildScan ->
    buildScan.buildScanPublished { publishedBuildScan ->
        // defer reading the `runNum` system property until execution time since it does not affect
        // the configuration of the build, and given its value changes between consecutive build invocations
        // it would always invalidate the configuration cache model from the first build invocation
        // in the second build invocation
        def getRunNumInputParam = { String name ->
            def envVarName = name.toUpperCase().replace('.', '_').replace('-', '_')
            return System.getProperty(name) ?: System.getenv(envVarName)
        }
        def runNum = getRunNumInputParam('com.gradle.enterprise.build-validation.runNum')
        def buildScanUri = publishedBuildScan.buildScanUri
        def buildScanId = publishedBuildScan.buildScanId
        def port = (buildScanUri.port != -1) ? ':' + buildScanUri.port : ''
        def baseUrl = "${buildScanUri.scheme}://${buildScanUri.host}${port}"

        def scanFile = new File(expDir, 'build-scans.csv')
        scanFile.append("${runNum},${baseUrl},${buildScanUri},${buildScanId}\n")
    }

    buildScan.onError { error ->
        def errorFile = new File(expDir, 'errors.txt')
        errorFile.text = 'Build Scan publishing failed.'
    }
}

// configure build scan publishing behavior
def configureBuildScanPublishing = { def buildScan ->
    buildScan.publishAlways()
    // buildScan.captureTaskInputFiles = true // too late to be set here for Gradle 5, set via sys prop
    if (buildScan.metaClass.respondsTo(buildScan, 'setUploadInBackground', Boolean)) buildScan.uploadInBackground = false // uploadInBackground not available for build-scan-plugin 1.16
}

// add custom data identifying the experiment
def addBuildScanCustomData = { def buildScan ->
    addCustomValueAndSearchLink(buildScan, "Experiment id", expId)
    buildScan.tag(expId)

    addCustomValueAndSearchLink(buildScan, "Experiment run id", runId)

    buildScan.value("Build validation scripts", scriptsVersion)
}

// fail if no GE server is configured
def logErrorMissingGradleEnterpriseServerURL = {
    def errorFile = new File(expDir, 'errors.txt')
    errorFile.text = 'The Gradle Enterprise server URL has not been configured in the project or on the command line.'
}

// fail if the GE / Build Scan plugin is not applied
def logErrorMissingGradleEnterprisePlugin = { def plugin ->
    def errorFile = new File(expDir, 'errors.txt')
    errorFile.text = "The $plugin plugin is missing from the project."
}

// do not fail if the CCUD plugin is not applied but surface a warning
def logWarningMissingCommonCustomUserDataGradlePlugin = {
    def warningFile = new File(expDir, 'warnings.txt')
    warningFile.append("The com.gradle.common-custom-user-data-gradle-plugin plugin is missing from " +
            "the project (see https://github.com/gradle/common-custom-user-data-gradle-plugin).\n")
}

// configure build scan behavior and optionally apply the GE / Build Scan / CCUD plugin
if (GradleVersion.current() < GradleVersion.version('6.0')) {
    //noinspection GroovyAssignabilityCheck
    rootProject {
        buildscript.configurations.getByName("classpath").incoming.afterResolve { ResolvableDependencies incoming ->
            def resolutionResult = incoming.resolutionResult

            def scanPluginComponent = resolutionResult.allComponents.find {
                it.moduleVersion.with { group == "com.gradle" && (name == "build-scan-plugin" || name == "gradle-enterprise-gradle-plugin") }
            }

            if (gePluginVersion) {
                if (!scanPluginComponent) {
                    logger.quiet("Applying $BUILD_SCAN_PLUGIN_CLASS via init script")
                    logger.quiet("Connection to Gradle Enterprise: $geUrl, allowUntrustedServer: $geAllowUntrustedServer")
                    pluginManager.apply(initscript.classLoader.loadClass(BUILD_SCAN_PLUGIN_CLASS))
                    if (geUrl) buildScan.server = geUrl
                    if (geAllowUntrustedServer) buildScan.allowUntrustedServer = parseBoolean(geAllowUntrustedServer)
                }
            } else {
                if (!scanPluginComponent) {
                    logErrorMissingGradleEnterprisePlugin('com.gradle.build-scan')
                    throw new IllegalStateException("The com.gradle.build-scan plugin is missing from the project.\n" +
                            "Either apply it directly (see https://docs.gradle.com/enterprise/gradle-plugin/#gradle_5_x) to the project,\n" +
                            "or use `--enable-gradle-enterprise` when running the build validation script.")
                }
            }

            def ccudPluginComponent = resolutionResult.allComponents.find {
                it.moduleVersion.with { group == "com.gradle" && name == "common-custom-user-data-gradle-plugin" }
            }

            if (ccudPluginVersion && atLeastGradle4) {
                if (!ccudPluginComponent) {
                    logger.quiet("Applying $CCUD_PLUGIN_CLASS via init script")
                    pluginManager.apply(initscript.classLoader.loadClass(CCUD_PLUGIN_CLASS))
                }
            } else {
                if (!ccudPluginComponent) {
                    logWarningMissingCommonCustomUserDataGradlePlugin()
                }
            }
        }

        pluginManager.withPlugin(BUILD_SCAN_PLUGIN_ID) {
            afterEvaluate {
                if (geUrl) buildScan.server = geUrl
                if (geAllowUntrustedServer) buildScan.allowUntrustedServer = parseBoolean(geAllowUntrustedServer)

                if (!buildScan.server && !omitServerUrlValidation) {
                    logErrorMissingGradleEnterpriseServerURL()
                    throw new IllegalStateException("The Gradle Enterprise server URL is not configured.\n" +
                            "Either configure it directly (see https://docs.gradle.com/enterprise/gradle-plugin/#gradle_5_x_2) in the project,\n" +
                            "or use `--gradle-enterprise-server` when running the build validation script.")
                }

                registerBuildScanActions(buildScan)
                configureBuildScanPublishing(buildScan)
                addBuildScanCustomData(buildScan)
            }
        }
    }
} else {
    gradle.settingsEvaluated { settings ->
        if (gePluginVersion) {
            if (!settings.pluginManager.hasPlugin(GRADLE_ENTERPRISE_PLUGIN_ID)) {
                logger.quiet("Applying $GRADLE_ENTERPRISE_PLUGIN_CLASS via init script")
                logger.quiet("Connection to Gradle Enterprise: $geUrl, allowUntrustedServer: $geAllowUntrustedServer")
                settings.pluginManager.apply(initscript.classLoader.loadClass(GRADLE_ENTERPRISE_PLUGIN_CLASS))
                extensionsWithPublicType(settings, GRADLE_ENTERPRISE_EXTENSION_CLASS).collect { settings[it.name] }.each { ext ->
                    if (geUrl) ext.server = geUrl
                    if (geAllowUntrustedServer) ext.allowUntrustedServer = parseBoolean(geAllowUntrustedServer)
                }
            }
        } else {
            if (!settings.pluginManager.hasPlugin(GRADLE_ENTERPRISE_PLUGIN_ID)) {
                logErrorMissingGradleEnterprisePlugin('com.gradle.enterprise')
                throw new IllegalStateException("The com.gradle.enterprise plugin is missing from the project.\n" +
                        "Either apply it directly (see https://docs.gradle.com/enterprise/gradle-plugin/#gradle_6_x_and_later),\n" +
                        "or use `--enable-gradle-enterprise` when running the build validation script.")
            }
        }

        if (ccudPluginVersion) {
            if (!settings.pluginManager.hasPlugin(CCUD_PLUGIN_ID)) {
                logger.quiet("Applying $CCUD_PLUGIN_CLASS via init script")
                settings.pluginManager.apply(initscript.classLoader.loadClass(CCUD_PLUGIN_CLASS))
            }
        } else {
            if (!settings.pluginManager.hasPlugin(CCUD_PLUGIN_ID)) {
                logWarningMissingCommonCustomUserDataGradlePlugin()
            }
        }

        extensionsWithPublicType(settings, GRADLE_ENTERPRISE_EXTENSION_CLASS).collect { settings[it.name] }.each { ext ->
            if (geUrl) ext.server = geUrl
            if (geAllowUntrustedServer) ext.allowUntrustedServer = parseBoolean(geAllowUntrustedServer)

            if (!ext.server && !omitServerUrlValidation) {
                logErrorMissingGradleEnterpriseServerURL()
                throw new IllegalStateException("The Gradle Enterprise server URL is not configured.\n" +
                        "Either configure it directly (see https://docs.gradle.com/enterprise/gradle-plugin/#gradle_6_x_and_later_2) in the project,\n" +
                        "or use `--gradle-enterprise-server` when running the build validation script.")
            }

            registerBuildScanActions(ext.buildScan)
            configureBuildScanPublishing(ext.buildScan)
            addBuildScanCustomData(ext.buildScan)
        }
    }
}

static def extensionsWithPublicType(def container, String publicType) {
    container.extensions.extensionsSchema.elements.findAll { it.publicType.concreteClass.name == publicType }
}

static boolean isNotAtLeast(String versionUnderTest, String referenceVersion) {
    GradleVersion.version(versionUnderTest) < GradleVersion.version(referenceVersion)
}

static void addCustomValueAndSearchLink(buildScan, String label, String value) {
    buildScan.value(label, value)
    if (buildScan.metaClass.respondsTo(buildScan, 'getServer') && buildScan.server) { // required for Gradle 4.x / build-scan-plugin 1.16
        String server = buildScan.server
        String searchParams = "search.names=" + urlEncode(label) + "&search.values=" + urlEncode(value)
        String url = appendIfMissing(server, "/") + "scans?" + searchParams + "#selection.buildScanB=" + urlEncode("{SCAN_ID}")
        buildScan.link(label + " build scans", url)
    }
}

static String appendIfMissing(String str, String suffix) {
    return str.endsWith(suffix) ? str : str + suffix
}

static String urlEncode(String str) {
    return URLEncoder.encode(str, StandardCharsets.UTF_8.name())
}
